; 							ADOREMUS IN AETEMUM SANTICCIMUM SACRAMENTUM

; *** File Name: ACOSmain.asm
; *** Project: Automatic Change Over Switch for 3 phases and generator sypplies with 4 Priority levels
; *** Author: Ifediora Elochukwu C. @ FEDYRONIX INC.
; *** Date: 13/11/2014
; *** Processor: Atmel AT89C1051 Microcontroller (Compatible With MCS-51)
; *** Priority Switch: 4 DIP SW
; *** Phase Selector Driver: ULN2001

; A PROGRAM THAT MONITORS THE PHASE INDISCATORS AND SWITCHES THE PHASE SELECTORS ACCORDINGLY 
; (ie BASED ON PRIORITIES SET BY THE USER)

; ====================================== DEFINITIONS ==================================================
; FLAGS:
;  				PTIF	BIT		00H				; PRIORITY TEST INDICATION FLAG
;				PISCF	BIT		01H				; PHASE INDICATORS' STATE CHANGED FLAG

; MEMORY LOCATIONS:
; 				TM1		EQU		30H				; TEMPORARY MEMORY FOR DIP SWITCHES
;				PL1M	EQU		31H				; PRIORITY LEVEL 1 MEMORY
;				PL2M	EQU		32H				; PRIORITY LEVEL 2 MEMORY
;				PL3M	EQU		33H				; PRIORITY LEVEL 3 MEMORY
;				PL4M	EQU		34H				; PRIORITY LEVEL 4 MEMORY
;				PPISM	EQU		35H				; PREVIOUS PHASE INDICATOR STATE MEMORY
;				DM1		EQU		36H				; DELAY MEMORY 1
;				DM2		EQU		37H				; DELAY MEMORY 2
;				TM2		EQU		38H				; TEMPORARY MEMORY FOR THE PREVIOUS VALUES OF THE DIP SWs
;				TDM1	EQU		39H				; TIME DELAY MEMORY 1
;				TDM2	EQU		3AH				; TIME DELAY MEMORY 2
;				TDM3	EQU		3BH				; TIME DELAY MEMORY 3

; ========================================== MAIN =====================================================

				ORG		0000H
				JMP		MAIN					; ON SYSTEM RESET, JUMP TO MAIN
				ORG		0003H
				JMP		PRIORITY_SETUP			; ON SYSTEM EXTERNAL INTERRUPT 0, JUMP TO THE ISR 'PRIORITY_SETUP'

; =====================================================================================================
				ORG		0006H					; START AT THIS ADDRESS
MAIN:			
				SETB	IT0						; CHOOSE NEGATIVE EGDE INTERRUPT TYPE FOR EXTERNAL INTERRUPT 0				
				MOV 	IE, #10000001B			; ENABLE THE !INT0 EXTERNAL INTERRUPT (PRIORITY_SETUP ISR)

				MOV		P1, #0FH				; MAKE THE LSB OF P1 INPUT PORTS
				CLR		P3.3					; TURN OFF THE LED TO SHOW THAT THE PRIORITY SETUP IS NOT BEING PROCESSED
				JNB		00H, $					; STAY HERE UNLESS THE PRIORITY LEVEL HAS BEEN RESET AT LEAST ONCE
REPEAT:			MOV		38H, #04H				; COUNTER FOR THE FOUR MEMORIES (THE NATURE OF THE PROGRAM MADE ME TO LOAD THREE)
				CLR		01H

				MOV		R0, #31H				; LOAD IMMEDIATE THE FIRST MEMORY LOCATION
RP_1:			MOV		A,@R0					; LOAD THE CONTENT OF THE PRIORITY LEVEL MEMORY
				CALL 	COMPARATOR	
				JB		01H, REPEAT				; START AFRESH WHEN A NEW STATE IS DETECTED
				INC		R0						; NEXT PRIORITY LEVEL MEMORY LOCATION
				DJNZ	38H, RP_1				; COUNTER TAKE NOTE
				SJMP	REPEAT					; START AFRESH

;======================================================================================================
R_TURN_OFF_GEN:	
				MOV		C, P1.0					; READ THE RED PHASE INDICATOR PIN
				JNC		EXIT_R_TURN_OFF_GEN		; EXIT THE SR IF THERE IS NO SUPPLY
				CLR		P1.7					; TURN OFF THE GENERATOR
				CLR		P1.5					; CLEAR ALL (TURN OFF) OTHER PIN LINES/PHASE SWITCHES
				CLR		P1.6					
				ACALL	DELAY					; CALL DELAY FOR PROTECTION
				SETB	P1.4					; CONNECT THE RED PHASE
RED:			ACALL	PI_STATES				; STAYS IN THE LOOP TILL THE STATE OF ANY PHASE INDICATOR CHANGES
				JNB		01H, RED
				CLR		P1.4					; ELSE RED PHASE IS OFF, DISCONNECT	R
EXIT_R_TURN_OFF_GEN:
				RET
; ----------------------------------------------
Y_TURN_OFF_GEN:	
				MOV		C, P1.1					; READ THE YELLOW PHASE INDICATOR PIN
				JNC		EXIT_Y_TURN_OFF_GEN		; EXIT THE SR IF THERE IS NO SUPPLY
				CLR		P1.7					; TURN OFF THE GENERATOR
				CLR		P1.6					; CLEAR ALL (TURN OFF) OTHER PIN LINES/PHASE SWITCHES
				CLR		P1.4					
				ACALL	DELAY					; CALL DELAY FOR PROTECTION
				SETB	P1.5					; CONNECT THE YELLOW PHASE
YELLOW:			ACALL	PI_STATES				; STAYS IN THE LOOP TILL THE STATE OF ANY PHASE INDICATOR CHANGES
				JNB		01H, YELLOW
				CLR		P1.5					; ELSE YELLOW PHASE IS OFF, DISCONNECT Y
EXIT_Y_TURN_OFF_GEN:
				RET
; ----------------------------------------------
B_TURN_OFF_GEN:
				MOV		C, P1.2					; READ THE BLUE PHASE INDICATOR PIN
				JNC		EXIT_B_TURN_OFF_GEN		; EXIT THE SR IF THERE IS NO SUPPLY
				CLR		P1.7					; TURN OFF THE GENERATOR
				CLR		P1.5					; CLEAR ALL (TURN OFF) OTHER PIN LINES/PHASE SWITCHES
				CLR		P1.4
				ACALL	DELAY					; CALL DELAY FOR PROTECTION
				SETB	P1.6					; CONNECT THE BLUE PHASE
BLUE:			ACALL	PI_STATES				; STAYS IN THE LOOP TILL THE STATE OF ANY PHASE INDICATOR CHANGES
				JNB		01H, BLUE
				CLR		P1.6					; ELSE BLUE PHASE IS OFF, DISCONNECT B
EXIT_B_TURN_OFF_GEN:
				RET
; ----------------------------------------------
TURN_OFF_ALL_EX_GEN:
//				; INCLUDE A CODE FOR ENSURING THAT IF ANY PHASE INDICATOR IS PULLED HIGH (ie ITS ON) SKIP TURNING ON GEN
//				MOV		A, P1					; READ PORT 1 TO CHECK INDICATORS THAT ARE ON
//				CLR		C
//CHECK:			RRC		A
//				JNC		CHECK
				; INCLUDE THE CODE TO TURN ON THE GENERATOR, HERE
				CLR		P1.4					; CLEAR ALL (TURN OFF) OTHER PIN LINES/PHASE SWITCHES
				CLR		P1.5					
				CLR		P1.6
				ACALL	DELAY					; CALL DELAY FOR PROTECTION
				SETB	P1.7					; CONNECT THE GENERATOR PHASE
GENERATOR:		ACALL	PI_STATES				; STAYS IN THE LOOP TILL THE STATE OF ANY PHASE INDICATOR CHANGES
				JNB		01H, GENERATOR
				CLR		P1.7					; ELSE BLUE PHASE IS OFF, DISCONNECT B

				RET

; ============================== DELAY USED FOR SAFETY/PROTECTION =====================================
DELAY:			
				MOV		39H, #0FFH 
J:				MOV		3AH, #0FFH
				DJNZ	3AH, $
				DJNZ	39H, J
				RET

; ================================ 20mS DELAY FOR DELAY IN DETECTION ===========================================
DELAY_20mS:
				MOV		39H, #3BH				; MOVE #0FFH INTO MEM LOCATIONS (3BH & 3CH) THAT SERVE AS COUNTER FOR THIS PARTICULAR
DLY_20mS_1:		MOV		3AH, #0A8H				; DELAY SUBROUTINE
				DJNZ	3AH, $					; STAY HERE TILL THE CONTENT OF THE MEMORY LOCATION (3CH) IS ZERO
				DJNZ	39H, DLY_20mS_1			; STAY HERE TILL THE CONTENT OF THE MEMORY LOCATION (3BH) IS ZERO

				RET

; ================================ 2S DELAY  FOR DEBOUNCING ===========================================
DELAY_2S:		MOV		3BH, #64H				; MOVE #64H (100DEC) INTO MEM LOC. 3DH FOR DELAY (100 x 20mS = 2S)
				; 20mS DELAY
DLY_2S_1:		MOV		39H, #3DH				; MOVE #0FFH INTO MEM LOCATIONS (3BH & 3CH) THAT SERVE 
DLY_2S_2:		MOV		3AH, #0A2H				; AS COUNTER FOR THIS PARTICULAR DELAY SUBROUTINE
				DJNZ	3AH, $					; STAY HERE TILL THE CONTENT OF THE MEMORY LOCATION (3CH) IS ZERO
				DJNZ	39H, DLY_2S_2			; STAY HERE TILL THE CONTENT OF THE MEMORY LOCATION (3BH) IS ZERO
				; --- END
				DJNZ	3BH, DLY_2S_1			; STAY HERE TILL THE CONTENT OF THE MEMORY LOCATION (3BH) IS ZERO

				RET

; ================================== DIP SWITCH STATES ================================================
DIP_SW:
				MOV		30H, P3					; MOVE THE PRESENT STATE OF THE DIP SWITCHES TO THE MEM LOCATION 30H
				MOV		A, 30H					; MOVE THE PRESENT STATE OF THE DIP SWITCHES TO THE ACCUMULATOR
				ANL		A, #10100011B			; MASK ALL THE BITS/PINS EXCEPT THOSE CONNECTED TO THE DIP SWITCHES
				RET

; =================================== PRIORITY SETUP ==================================================
PRIORITY_SETUP:
; THIS ISR SETS THE PRIORITY LEVELS OF THE DIFFERENT PHASES CONNECTED TO THE SYSTEM
; FOR NOW WE HAVE ONLY 3 PHASES(RYB) + GENERATOR SUPPLY
				CLR		EA						; DISABLE ALL INTERRUPTS
;				CLR		IE0						; CLEAR THE INTERRUPT FLAG FOR EXTERNAL INTERRUPT 0
				SETB	P3.3					; TURN ON THE LED TO SHOW THAT THE PRIORITY SETUP IS GOING ON
				; ---- ENSURES THAT ALL THE DIP SWITCHES ARE IN THE RESET STATE (ie #00) BEFORE SETUP IS CARRIED OUT
				CALL	DIP_SW					; READ THE PORT CONNECTED TO THE DIP SWITCHES
				CJNE	A, #00H, PRIORITY_SETUP	; CHECK IF THE INITIAL STATE OF THE DIP CHANGED
				; ---- END
PS_1:			CALL	DIP_SW
				CJNE	A, #00H, PS_1_X			; ENSURE THAT THE STATE OF THE DIP SWITCHES HAS CHANGED BEFORE CONTINUING
				SJMP	PS_1					
PS_1_X:			MOV		38H, A					; SAVE THE DIP SW STATE
				MOV		31H, A					; SAVE THE FIRST PRIORITY LINE

PS_2:			CALL	DIP_SW
				CJNE	A, 38H, PS_2_X			; ENSURE THAT THE STATE OF THE DIP SWITCHES HAS CHANGED BEFORE CONTINUING
				SJMP	PS_2					
PS_2_X:			MOV		38H, A					; SAVE THE DIP SW STATE
				XRL		A, 31H
				MOV		32H, A					; SAVE THE SECOND PRIORITY LINE

PS_3:			CALL	DIP_SW
				CJNE	A, 38H, PS_3_X			; ENSURE THAT THE STATE OF THE DIP SWITCHES HAS CHANGED BEFORE CONTINUING
				SJMP	PS_3					
PS_3_X:			MOV		38H, A					; SAVE THE DIP SW STATE
				XRL		A, 31H
				XRL		A, 32H
				MOV		33H, A					; SAVE THE THIRD PRIORITY LINE

PS_4:			CALL	DIP_SW
				CJNE	A, 38H, PS_4_X			; ENSURE THAT THE STATE OF THE DIP SWITCHES HAS CHANGED BEFORE CONTINUING
				SJMP	PS_4					
PS_4_X:			MOV		38H, A					; SAVE THE DIP SW STATE
				XRL		A, 31H
				XRL		A, 32H
				XRL		A, 33H
				MOV		34H, A					; SAVE THE FOURTH/LAST PRIORITY LINE
				
				SETB	00H						; INDICATE THAT A RESET/CHANGE IN THE PRIORITY LEVEL HAS BEEN CARRIED OUT
				SETB	P3.2					; MAKE P3.2 AN INPUT PORT NOW
				JB		P3.2, $					; STAY HERE TILL THE USER INDICATES THAT HE'S DONE (A NORMALLY-ON PIN)
				CLR		P3.3					; TURN OFF THE LED TO SHOW THAT THE PRIORITY SETUP HAS FINISHED
				CALL	DELAY_2S				; FOR DEBOUNCE PROTECTION FOR THE KEY THAT HAS DUAL PURPOSE (P3.2)
				CLR		IE0						; CLEAR THE INTERRUPT FLAG FOR EXTERNAL INTERRUPT 0
				SETB	EA						; ENABLE ALL INTERRUPTS
				RETI

; =================================== COMPARATOR ==================================================
COMPARATOR:
; THE CONTENT OF THE ACCUMULATOR ON ENTERING THIS SR IS EXPECTED TO HAVE THE CONTENT OF ANY OF THE FOLLOWING
; MEMORY LOCATIONS (31H, 32H, 33H, 34H)
				CJNE	A, #00000001B, C1
				CALL	R_TURN_OFF_GEN
				SJMP	EXIT_C 
C1:	 			CJNE	A, #00000010B, C2
				CALL	Y_TURN_OFF_GEN
				SJMP	EXIT_C 
C2:				CJNE	A, #00100000B, C3
				CALL	B_TURN_OFF_GEN
				SJMP	EXIT_C 
C3:				CJNE	A, #10000000B, EXIT_C
				CALL	TURN_OFF_ALL_EX_GEN
EXIT_C:			
				RET

; =================================== PI_STATES ==================================================
PI_STATES:	
; THIS IS AN OSR THAT IS CALLED CONTINUALY TO CHECK THE STATE OF THE PHASE INDICATORS	
				MOV		A, P1 					; READ THE PORT THAT WAS INTERFACED TO PHASE INDICATORS 
				ANL		A, #00001111B			; MASK THE PINS THAT ARE NOT INTERFACED TO THE PHASE INDICATORS
				MOV		35H, A					; SAVE THE NEW STATE OF THE PHASE INDICATORS
				; SEE IF IT WOULD BE NECESSARY TO ADD A DELAY HERE FOR CHANGE IN STATE TO BE DETECTED
				CALL	DELAY_20mS				; FOR DEBOUNCE PROTECTION FOR THE KEY THAT HAS DUAL PURPOSE (P3.2)
				MOV		A, P1 					; AGAIN, READ THE PORT THAT WAS INTERFACED TO PHASE INDICATORS 
				ANL		A, #00001111B			; MASK THE PINS THAT ARE NOT INTERFACED TO THE PHASE INDICATORS
				CJNE	A, 35H, PIS				; CHECK THE STATE OF THE PHASE INDICATORS. IF CHANGED SET FLAG AND EXIT ELSE JUST EXIT 
				SJMP 	EXIT_PI_STATES
				
PIS:			SETB	01H						; SET FLAG TO INDICATE THAT THE PHASE INDICATORS' STATE HAS CHANGED

EXIT_PI_STATES:	RET

				END